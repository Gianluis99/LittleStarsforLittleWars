
arco(Y,X):-arco(X,Y).

%guess dato un nodo vicino o l impossesso oppure no
possesso(Nodo,Squadra,Nodo1) | nopossesso(Nodo,Squadra,Nodo1):-possesso(Nodo1,Squadra,_),
arco(Nodo1,Nodo),nodo(Nodo,_,_).

%non è possibile che il costo sia maggiore 
:-nodo(Nodo,Costo,_),possesso(Nodo,Squadra,_),nodo(Nodo1,Costo1,_),possesso(Nodo1,Squadra,Nodo),
Costo1>=Costo.

%se un nodo è gia impossessato dalla medesima squadra non dovrà conquistarlo
:-nodo(Nodo,Costo,_),possesso(Nodo,Squadra,_),nodo(Nodo1,Costo1,_),possesso(Nodo1,Squadra,-1),
possesso(Nodo1,Squadra,Nodo).

%si impossessa primariamente di un nodo nemico vicino
:~possesso(Nodo,Squadra,-2),possesso(Nodo1,Squadra1,_),arco(Nodo,Nodo1),
not possesso(Nodo1,Squadra,Nodo),Squadra!=Squadra1.[3@7,Nodo1]


%si impossessa prima di tutto del nodo che ha il livello piu alto
%se il nodo rimane fuori allora pago per il livello, quindi piu è alto il livello piu pago
:~ out(Nodo),nodo(Nodo,Costo,Livello).[Livello@6,Nodo]
out(Nodo):-possesso(Nodo1,Squadra,-2),nodo(Nodo,_,_),not possesso(Nodo,Squadra,Nodo1).


%successivamente si impossessa  del nodo che ha il costo minore,quindi piu è alto il costo piu paghiamo
:~possesso(Nodo1,Squadra,_),possesso(Nodo,Squadra,Nodo1),nodo(Nodo,Costo,_).[Costo@5,Nodo]


%incentivo ad impossessarsi di tutti i nodi possibili
%se abbiamo un nodo collegato che ha costo minore non vorrei che non fosse impossessato
:~nodo(Nodo,Costo,_),possesso(Nodo,Squadra,-2),arco(Nodo,Nodo1),nodo(Nodo1,Costo1,_),
not possesso(Nodo1,Squadra,Nodo).[3@8,Nodo1]


%solo il nodo principale puo impossessarsi di altri nodi
:-possesso(Nodo,Squadra,IdPossesso),possesso(Nodo1,Squadra,Nodo),IdPossesso!=-2.

%si impossessa solo di un nodo per volta
:- #count{A,Nodo:possesso(Nodo,Squadra,-2),possesso(A,B,Nodo) }>1.



%SECONDA PARTE il nodo manda le navi ai vicini amici

%il nodo manderà le navi ad un nodo vicino se ha tutti i vicini amici
mandaNavi(Nodo,Nodo1,Navi)|nomandaNavi(Nodo,Nodo1,Navi):-nodo(Nodo,Navi,_),nodo(Nodo1,_,_),
arco(Nodo,Nodo1),
tuttiViciniAmici(Nodo).

%un nodo non puo mandare le navi a se stesso
:-mandaNavi(Nodo,Nodo,_).


%contiamo tutti i nodi vicini, se questo conteggio è uguale ai nodi vicini che hanno la stessa squadra
%allora tutti i nodi vicini sono della stessa squadra
tuttiViciniAmici(ID):-nodo(ID,_,_),#count{ID2,ID :arco(ID,ID2) }=A,
#count{ID2,ID :possesso(ID,Squadra,-2),possesso(ID2,Squadra,_),arco(ID,ID2)}=S,A=S.

%non puo mandare le navi se ha un costo =0
:-nodo(Nodo,Costo,_),mandaNavi(Nodo,_,Costo),Costo=0.

%manda le navi solo ad un nodo per volta
:-#count{B:mandaNavi(A,B,C) }>1.



%il nodo manda le navi primariamente ai nodi amici che hanno un vicino dell altra squadra(anche ricorsivo)
:~nodo(Nodo,Costo,_),arco(Nodo,Nodo1),nodo(Nodo1,_,_),haUnNemicoVicino(Nodo1),
 not mandaNavi(Nodo,Nodo1,Costo).[3@4,Nodo,Nodo1]

%ricorsione 
:~nodo(Nodo,Costo,_),raggiunge(Nodo,Nodo1),nodo(Nodo1,_,_),haUnNemicoVicino(Nodo1),
not mandaNavi(Nodo,Nodo1,Costo).[3@4,Nodo,Nodo1]


haUnNemicoVicino(Nodo):-possesso(Nodo,Squadra,_),possesso(Nodo1,Squadra1,_),arco(Nodo,Nodo1),
Nodo!=Nodo1,Squadra!=Squadra1.

%un nodo è collegato con un altro nodo se c’è un arco che li collega
nodoCollegato(Nodo,Nodo1):-nodo(Nodo,_,_),nodo(Nodo1,_,_),arco(Nodo,Nodo1).

%un nodo raggiunge un altro nodo  se sono collegati da un altro nodo
raggiunge(NodoX,NodoY):-nodoCollegato(NodoX,NodoZ),nodoCollegato(NodoZ,NodoY).





%successivamente il nodo manda le navi ai nodi che hanno un basso livello
%per questo piu è alto il livello piu paghiamo
:~nodo(Nodo,Costo,_),nodo(Nodo1,_,Livello),mandaNavi(Nodo,Nodo1,_).[Livello@2,Nodo1]

%a parità di livello manda le navi ai nodi che hanno meno navi
%per questo piu sono alte le navi piu paghiamo
:~nodo(Nodo,Costo,_),nodo(Nodo1,Navi,_),mandaNavi(Nodo,Nodo1,_).[Navi@1,Nodo1]

%incentivo a mandare le navi se ha tutti i nodi vicini amici
:~nodo(Nodo,Costo,_),tuttiViciniAmici(Nodo), nomandaNavi(Nodo,Nodo1,_).[Costo@3,Nodo1,Nodo]




%in una passata non è possibile che il nodo si impossessi prima di un nodo e poi mandi delle navi 
%in quanto i dati si devono aggiornare e li aggiorneremo tramite codice..
%altrimenti lui potrebbe conquistare un nodo e poi se ha tutti i nodi della stessa squadra
%manda le navi però non con i nodi aggiornati
:-nodo(Nodo,_,_),possesso(_,_,Nodo),mandaNavi(Nodo,_,_).


:-possesso(Nodo,Squadra,IdPossesso),mandaNavi(Nodo,_,_),IdPossesso!=-2.


